@echo off
setlocal enabledelayedexpansion

echo.
echo "ğŸ¤š  Initializing everything from scratch..."

:: Check if winget is available (should be on Windows 10 1709 or later, Windows 11)
where winget >nul 2>&1
if %errorlevel% neq 0 (
    echo "âŒ  winget not found. Please ensure you're running Windows 10 version 1709 or later, or Windows 11."
    echo "   You can download the latest from: https://apps.microsoft.com/store/detail/app-installer/9NBLGGH4NNS1"
    exit /b 1
) else (
    echo "âœ…  winget is already available, will use it to setup all the necessary packages"
)

:: Install git using winget
where git >nul 2>&1
if %errorlevel% neq 0 (
    echo "ğŸ‘Š  Installing git"
    winget install -e --id Git.Git
    if !errorlevel! neq 0 (
        echo "âŒ  Failed to install git"
        exit /b 1
    )
    echo "âœ…  git installed successfully"
) else (
    echo "âœ…  git already installed"
)

:: Install chezmoi using winget
where chezmoi >nul 2>&1
if %errorlevel% neq 0 (
    echo "ğŸ‘Š  Installing chezmoi"
    winget install -e --id twpayne.chezmoi
    if !errorlevel! neq 0 (
        echo "âŒ  Failed to install chezmoi"
        exit /b 1
    )
    echo "âœ…  chezmoi installed successfully"
) else (
    echo "âœ…  chezmoi already installed"
)

:: Check if chezmoi is already initialized
if exist "%USERPROFILE%\.local\share\chezmoi\.git" (
  echo "ğŸš¸  chezmoi already initialized"
  echo "    Reinitialize with: 'chezmoi init https://github.com/pkaramishev/dotfiles.git'"
) else (
  echo "ğŸš€  Initializing chezmoi"
  chezmoi init https://github.com/pkaramishev/dotfiles.git
)

echo.
echo "âœ…  Done."

echo "â¯ï¸  Next steps: "
echo "1ï¸âƒ£  TBD"

endlocal

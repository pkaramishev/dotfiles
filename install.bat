@echo off
setlocal enabledelayedexpansion
chcp 65001>nul

echo.
echo ğŸ¤š  Initializing everything from scratch...

:: Check if winget is available (should be on Windows 10 1709 or later, Windows 11)
where winget >nul 2>&1
if %errorlevel% neq 0 (
    echo âŒ  winget not found. Please ensure you're running Windows 10 version 1709 or later, or Windows 11.
    echo â„¹ï¸  You can download the latest from: https://apps.microsoft.com/store/detail/app-installer/9NBLGGH4NNS1
    exit /b 1
) else (
    echo âœ…  winget is already available, will use it to setup all the necessary packages
)

:: Install git using winget
where git >nul 2>&1
if %errorlevel% neq 0 (
    echo ğŸ‘Š  Installing git
    winget install --silent --accept-package-agreements --accept-source-agreements -e --id Git.Git
    if !errorlevel! neq 0 (
        echo âŒ  Failed to install git. Check if it's already installed and `git` console command is available
        exit /b 1
    )
    echo âœ…  git installed successfully
) else (
    echo âœ…  git already installed
)

:: Install chezmoi using winget
where chezmoi >nul 2>&1
if %errorlevel% neq 0 (
    echo ğŸ‘Š  Installing chezmoi
    winget install --silent --accept-package-agreements --accept-source-agreements -e --id twpayne.chezmoi
    if !errorlevel! neq 0 (
        echo âŒ  Failed to install chezmoi. Check if it's already installed and `chezmoi` console command is available
        exit /b 1
    )
    echo âœ…  chezmoi installed successfully
) else (
    echo âœ…  chezmoi already installed
)

:: Initialize chezmoi from scratch
echo ğŸš€  Initializing chezmoi
start "" "%ProgramFiles%\Git\git-bash.exe" --login -i -c "chezmoi init --apply --purge https://github.com/pkaramishev/dotfiles.git; exec bash"

echo âœ…  Done

endlocal
